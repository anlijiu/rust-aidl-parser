grammar<'err>(
    lookup: &line_col::LineColLookup<'input>,
    diagnostics: &'err mut Vec<Diagnostic>
);

use crate::ast;
use crate::diagnostic::{Diagnostic, DiagnosticKind};
use crate::javadoc;

pub File: Option<ast::File> = {
    <p:Package> <vi:Import*> <i:Item> => {
        i.map(|item| ast::File {
            package: p,
            imports: vi,
            item,
        })
    }
}

// package x.y.z;
pub Package: ast::Package = {
    PACKAGE <p1:@L> <name:QualifiedName> ";" <p2:@R> => {
        ast::Package {
            name,
            symbol_range: ast::Range::new(lookup, p1, p2),
        }
    }
}

// import x.y.z;
pub Import: ast::Package = {
    IMPORT <p1:@L> <name:QualifiedName> ";" <p2:@R> => {
        ast::Package {
            name,
            symbol_range: ast::Range::new(lookup, p1, p2),
        }
    }
}

// x OR x.y.z
pub QualifiedName: String = {
    <v:(<IDENT> ".")*> <n:IDENT> => {
        if v.is_empty() {
            n.to_owned()
        } else {
            format!("{}.{}", v.join("."), n)
        }
    }
}

pub Item: Option<ast::Item> = {
    <i:Interface> => Some(ast::Item::Interface(i)),
    <p:Parcelable> => Some(ast::Item::Parcelable(p)),
    <e:Enum> => Some(ast::Item::Enum(e)),
    ! =>? {
        diagnostics.push(Diagnostic::from_error_recovery("Invalid item", lookup, <>)?);
        Ok(None)
    },
}

pub Interface: ast::Interface = {
    <p0:@L>
    <annotations:AnnotationList>
    <fp1:@L> INTERFACE <sp1:@L> <s:IDENT> <sp2:@R> "{" <v:InterfaceElement*> "}" <fp2:@R> => {
        let elements: Vec<ast::InterfaceElement> = v.into_iter().flatten().collect();

        ast::Interface {
            name: s.into(),
            elements,
            annotations,
            doc: javadoc::get_javadoc(input, p0),
            full_range: ast::Range::new(&lookup, fp1, fp2),
            symbol_range: ast::Range::new(&lookup, sp1, sp2),
        }
    }
}

pub InterfaceElement: Option<ast::InterfaceElement> = {
    <m:Method> => Some(ast::InterfaceElement::Method(m)),
    <c:Const> => Some(ast::InterfaceElement::Const(c)),
    ! =>? {
        diagnostics.push(Diagnostic::from_error_recovery("Invalid interface element", lookup, <>)?);
        Ok(None)
    },
}

pub Parcelable: ast::Parcelable = {
    <p0:@L>
    <annotations:AnnotationList>
    <fp1:@L> PARCELABLE <sp1:@L> <s:IDENT> <sp2:@R> "{" <v:ParcelableMember*> "}" <fp2:@R> => {
        let members: Vec<ast::Member> = v.into_iter().flatten().collect();

        ast::Parcelable {
            name: s.into(),
            members,
            annotations,
            doc: javadoc::get_javadoc(input, p0),
            full_range: ast::Range::new(&lookup, fp1, fp2),
            symbol_range: ast::Range::new(&lookup, sp1, sp2),
        }
    }
}

pub ParcelableMember: Option<ast::Member> = {
    <m:Member> => Some(m),
    ! =>? {
        diagnostics.push(Diagnostic::from_error_recovery("Invalid member", lookup, <>)?);
        Ok(None)
    },
}

pub Enum: ast::Enum = {
    <p0:@L>
    <annotations:AnnotationList>
    <fp1:@L> ENUM <sp1:@L> <s:IDENT> <sp2:@R> "{" <v:CommaSeparated<EnumElementOpt>> "}" <fp2:@R> => {
        let elements: Vec<ast::EnumElement> = v.into_iter().flatten().collect();

        ast::Enum {
            name: s.into(),
            elements,
            annotations,
            doc: javadoc::get_javadoc(input, p0),
            full_range: ast::Range::new(&lookup, fp1, fp2),
            symbol_range: ast::Range::new(&lookup, sp1, sp2),
        }
    }
}

pub EnumElementOpt: Option<ast::EnumElement> = {
    <el:EnumElement> => Some(el),
    ! =>? {
        diagnostics.push(Diagnostic::from_error_recovery("Invalid enum element", lookup, <>)?);
        Ok(None)
    },
}

pub Method: ast::Method = {
    <p0:@L>
    <annotations:AnnotationList>
    <fp1:@L> <oneway:ONEWAY?> <rt:Type>
    <sp1:@L> <n:IDENT> <sp2:@R>
    "(" <args:CommaSeparated<Arg>> ")"
    ("=" NUMBER)?  // TODO: only [0-9]+
    ";" <fp2:@R> => {
        ast::Method {
            oneway: oneway.is_some(),
            name: n.to_owned(),
            return_type: rt,
            args,
            annotations,
            doc: javadoc::get_javadoc(input, p0),
            full_range: ast::Range::new(&lookup, fp1, fp2),
            symbol_range: ast::Range::new(&lookup, sp1, sp2),
        }
    }
}

pub Arg: ast::Arg = {
    <p0:@L>
    <annotations:AnnotationList>
    <d:Direction> <t:Type> <n:IDENT?> => {
        ast::Arg {
            direction: d,
            name: n.map(str::to_owned),
            arg_type: t,
            annotations,
            doc: javadoc::get_javadoc(input, p0),
        }
    }
}

pub Direction: ast::Direction = {
    <d:DIRECTION?> => {
        match d {
            Some("in") => ast::Direction::In,
            Some("out") => ast::Direction::Out,
            Some("inout") => ast::Direction::InOut,
            None => ast::Direction::Unspecified,
            _ => unreachable!(),
        }
    }
}

pub Const: ast::Const = {
    <p0:@L>
    <annotations:AnnotationList>
    <fp1:@L> CONST <t:Type>
    <sp1:@L> <n:IDENT> <sp2:@R>
    "=" <v:Value>
    ";" <fp2:@R> => {
        ast::Const {
            name: n.to_owned(),
            const_type: t,
            value: v.to_owned(),
            annotations,
            doc: javadoc::get_javadoc(input, p0),
            full_range: ast::Range::new(&lookup, fp1, fp2),
            symbol_range: ast::Range::new(&lookup, sp1, sp2),
        }
    },
}

pub Member: ast::Member = {
    <p0:@L>
    <annotations:AnnotationList>
    <fp1:@L> <t:Type>
    <sp1:@L> <n:IDENT> <sp2:@R>
    <v:("=" <Value>)?>
    ";" <fp2:@R> => {
        ast::Member {
            name: n.to_owned(),
            member_type: t,
            value: v.map(str::to_owned),
            annotations,
            doc: javadoc::get_javadoc(input, p0),
            full_range: ast::Range::new(&lookup, fp1, fp2),
            symbol_range: ast::Range::new(&lookup, sp1, sp2),
        }
    }
}

pub EnumElement: ast::EnumElement = {
    <p0:@L>
    <annotations:AnnotationList>
    <fp1:@L>
    <sp1:@L> <n:IDENT> <sp2:@R>
    <v:("=" <Value>)?>
    <fp2:@R> => {
        ast::EnumElement {
            name: n.to_owned(),
            value: v.map(str::to_owned),
            doc: javadoc::get_javadoc(input, p0),
            full_range: ast::Range::new(&lookup, fp1, fp2),
            symbol_range: ast::Range::new(&lookup, sp1, sp2),
        }
    }
}

pub Type: ast::Type = {
    TypeVoid,
    TypePrimitive,
    TypeString,
    TypeArray,
    TypeList,
    TypeMap,
    TypeCustom,
}

pub TypeVoid: ast::Type = {
    <p1:@L> <n:VOID> <p2:@R> =>
        ast::Type::simple_type(n, ast::TypeKind::Void, lookup, p1, p2)
}

pub TypePrimitive: ast::Type = {
    <p1:@L> <n:PRIMITIVE> <p2:@R> =>
        ast::Type::simple_type(n, ast::TypeKind::Primitive, lookup, p1, p2)
}

pub TypeString: ast::Type = {
    <p1:@L> <n:STRING> <p2:@R> =>
        ast::Type::simple_type(n, ast::TypeKind::String, lookup, p1, p2)
}

pub TypeArray: ast::Type = {
    <p1:@L> <pp1:@L> <p:TypeArrayParameterValid> <pp2:@R> "[" "]" <p2:@L> => {
        ast::Type::array(p, &lookup, p1, p2)
    },
    <p1:@L> <pp1:@L> <p:TypeArrayParameterInvalid> "[" "]" <pp2:@R> <p2:@L> => {
        diagnostics.push(Diagnostic {
            kind: DiagnosticKind::Error,
            text: format!("Invalid array parameter ({}): must be a primitive or an enum", p.name),
            range: ast::Range::new(&lookup, pp1, pp2),
        });

        ast::Type::invalid_with_generics("Array", &[p], &lookup, p1, p2)
    },
}

TypeArrayParameterValid = {
    TypePrimitive,
    TypeCustom,
}

TypeArrayParameterInvalid = {
    TypeVoid,
    TypeString,
    TypeArray,
    TypeList,
    TypeMap,
}

pub TypeList: ast::Type = {
    <p1:@L> LIST "<" <p:TypeCollectionParameterValid> ">" <p2:@R> => {
        ast::Type::list(p, &lookup, p1, p2)
    },
    <p1:@L> LIST "<" <pp1:@L> <p:TypeCollectionParameterInvalid> <pp2:@R> ">" <p2:@R> => {
        diagnostics.push(Diagnostic {
            kind: DiagnosticKind::Error,
            text: format!("Invalid list parameter ({}): must be an object", p.name),
            range: ast::Range::new(&lookup, pp1, pp2),
        });
        ast::Type::invalid_with_generics("List", &[p], &lookup, p1, p2)
    },
}

pub TypeMap: ast::Type = {
    <p1:@L> MAP "<"
        <k:TypeCollectionParameterValid> "," <v:TypeCollectionParameterValid>
    ">" <p2:@R> => {
        ast::Type::map(k, v, &lookup, p1, p2)
    },
    <p1:@L> MAP "<"
        <kp1:@L> <k:TypeCollectionParameterInvalid> <kp2:@R> ","
        <v:TypeCollectionParameterInvalid>
    ">"  <p2:@R> => {
        diagnostics.push(Diagnostic {
            kind: DiagnosticKind::Error,
            text: format!("Invalid map key ({}): key must be an object", k.name),
            range: ast::Range::new(&lookup, kp1, kp2),
        });
        ast::Type::invalid_with_generics("Map", &[k, v], &lookup, p1, p2)
    },
    <p1:@L> MAP "<" <pp1:@L>
        <k:TypeCollectionParameterInvalid> "," <v:TypeCollectionParameterValid>
    <pp2:@R> ">" <p2:@R> => {
        diagnostics.push(Diagnostic {
            kind: DiagnosticKind::Error,
            text: format!("Invalid map parameters ({}, {}): key and value must be objects", k.name, v.name),
            range: ast::Range::new(&lookup, pp1, pp2),
        });
        ast::Type::invalid_with_generics("Map", &[k, v], &lookup, p1, p2)
    },
    <p1:@L> MAP "<"
        <k:TypeCollectionParameterValid> ","
        <vp1:@L> <v:TypeCollectionParameterInvalid> <vp2:@R>
    ">" <p2:@R> => {
        diagnostics.push(Diagnostic {
            kind: DiagnosticKind::Error,
            text: format!("Invalid map value ({}): value must be an object", v.name),
            range: ast::Range::new(&lookup, vp1, vp2),
        });
        ast::Type::invalid_with_generics("Map", &[k, v], &lookup, p1, p2)
    },
}

TypeCollectionParameterValid = {
    TypeString,
    TypeList,
    TypeMap,
    TypeCustom,
}

TypeCollectionParameterInvalid = {
    TypeVoid,
    TypePrimitive,
    TypeArray,
}

pub TypeCustom: ast::Type = {
    <p1:@L> <n:QualifiedName> <p2:@R> => {
        ast::Type {
            name: n.to_owned(),
            kind: ast::TypeKind::Custom,
            generic_types: vec![],
            definition: None,
            symbol_range: ast::Range::new(&lookup, p1, p2),
        }
    }
}

pub AnnotationList: Vec<ast::Annotation> = {
    <v:Annotation*> => v.into_iter().flatten().collect()
}

pub Annotation: Option<ast::Annotation> = {
    <n:ANNOTATION> <v:("(" <CommaSeparated<AnnotationParam>> ")")?> => {
        Some(ast::Annotation {
            name: n.to_owned(),
            key_values: v.unwrap_or_default().into_iter().collect(),
        })
    },
    //! =>? {
    //    diagnostics.push(Diagnostic::from_error_recovery("Invalid annotation", lookup, <>)?);
    //    Ok(None)
    //},
}

AnnotationParam: (String, Option<String>) = {
    <k:IDENT> <v:("=" <Value>)?> => (k.to_owned(), v.map(str::to_owned))
}

pub Value: &'input str = {
    NUMBER,
    QUOTED_STRING,
    BOOLEAN,
    "{" "}" => "{}",
}

CommaSeparated<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
}

match {
    // Whitespaces, comments, EOL
    r"\s*" => { }, // The default whitespace skipping is disabled and an `ignore pattern` is specified
    r"//[^\n\r]*[\n\r]*" => { }, // Skip `// comments`
    r"/\*[^*]*\*+(?:[^/*][^*]*\*+)*/" => { },  // Skip `/* comments */`
    
    "package" => PACKAGE,
    "import" => IMPORT,
    "interface" => INTERFACE,
    "parcelable" => PARCELABLE,
    "enum" => ENUM,
    "oneway" => ONEWAY,
    "const" => CONST,
    
    r"(in|out|inout)" => DIRECTION,
    "void" => VOID,
    r"(byte|short|int|long|float|double|boolean|char)" => PRIMITIVE,
    r"(String|CharSequence)" => STRING,
    "List" => LIST,
    "Map" => MAP,
    
    r#""[^"\n\r]*""# => QUOTED_STRING,
    r#"(true|false)"# => BOOLEAN,

    r"@[a-zA-Z_][a-zA-Z0-9_]*" => ANNOTATION,
    
    // Signs
    ";",  ",", "{", "}", "(", ")", "[", "]", "<", ">", "=", ".", "-",
} else {
    // Other
    r"[a-zA-Z_][a-zA-Z0-9_]*" => IDENT,
    r"[+-]?(\d*\.)?\d+[f]?" => NUMBER,
} else {
    r"[0-9]" => DIGIT,
} else {
    //r"[^;]*;" => ANY_INTERFACE_ELEMENT,  // TODO
}
